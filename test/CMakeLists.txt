ENABLE_TESTING()

IF(RAPIDJSON_BUILD_THIRDPARTY_GTEST)
    SET(RAPIDJSON_GTEST_SOURCE_DIR "${CMAKE_SOURCE_DIR}/thirdparty/gtest")
    SET(RAPIDJSON_GTEST_INCLUDE_DIRS "${RAPIDJSON_GTEST_SOURCE_DIR}/include")
ELSE()
    FIND_PATH(RAPIDJSON_GTEST_SOURCE_DIR src/gtest_main.cc HINTS /usr/src/gtest)
    FIND_PATH(RAPIDJSON_GTEST_INCLUDE_DIRS gtest.h HINTS /usr/include/gtest)
ENDIF()

IF(RAPIDJSON_GTEST_SOURCE_DIR AND RAPIDJSON_GTEST_INCLUDE_DIRS)
    IF(WIN32 AND (NOT CYGWIN) AND (NOT MINGW))
        SET(gtest_disable_pthreads ON)
        SET(gtest_force_shared_crt ON)
    ENDIF()

    ADD_SUBDIRECTORY(${RAPIDJSON_GTEST_SOURCE_DIR} ${CMAKE_BINARY_DIR}/googletest)

    SET(GTEST_LIBRARIES gtest)
    SET(GTEST_MAIN_LIBRARIES gtest_main)
    SET(GTEST_BOTH_LIBRARIES ${GTEST_LIBRARIES} ${GTEST_MAIN_LIBRARIES})
ELSE()
    MESSAGE(SEND_ERROR "Cannot find GTest sources.")
ENDIF()

INCLUDE_DIRECTORIES(${RAPIDJSON_GTEST_INCLUDE_DIRS})

SET(TEST_LIBRARIES ${GTEST_BOTH_LIBRARIES})

ADD_CUSTOM_TARGET(tests ALL)
ADD_SUBDIRECTORY(perftest)
ADD_SUBDIRECTORY(unittest)
